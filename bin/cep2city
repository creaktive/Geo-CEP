#!/usr/bin/env perl
# ABSTRACT: Returns city data for a given CEP
# PODNAME: cep2city

=head1 SYNOPSIS

cep2city CEP [ CEP2 CEP3 ... ]

=head1 DESCRIPTION

Prints available details for a given CEP number:

    $ cep2city.pl 12420-010
    cep       	12420010
    city      	Pindamonhangaba
    ddd       	12
    lat       	-22.9166667
    lon       	-45.4666667
    state     	SP
    state_long	SÃ£o Paulo

=head1 SEE ALSO

=for :list
* L<Geo::CEP>
* L<WWW::Correios::CEP>

=cut

use strict;
use utf8;
use warnings qw(all);

use open qw(:std :utf8);

# VERSION

use Geo::CEP;
use Pod::Usage;

pod2usage(
    -exitval    => 0,
    -verbose    => 99,
) unless @ARGV;

my $gc = Geo::CEP->new;

for my $cep (@ARGV) {
    $cep =~ s/\D//gx;
    if ($cep && (my $r = $gc->find($cep))) {
        $r->{cep} = $cep;
        printf "%-10s\t%s\n", $_, $r->{$_} for sort keys %{$r};
        print "\n";
    } else {
        printf STDERR "'%s' not found!\n", $cep;
    }
}
